// fetch application ID
application_id = app_id;
link_header = "https://recruit.zoho.com/recruit/v2/Applications";
application_link = link_header.concat("/").concat(application_id);
//info application_link;
//
//
// pass request url to Zoho API
app_response = invokeurl
[
	url :application_link
	type :GET
	connection:"zohorecruitapp"
];
//info app_response;
//
//
// fetch important keys
appResponse = app_response.getJSON("data");
// fetch basic details
phone_no = appResponse.getJSON("WA_Number_API");
phNo = getAlphaNumeric(phone_no);
user_name = appResponse.getJSON("First_Name").replaceAll(" ","+");
full_name = appResponse.getJSON("Candidate_Full_Name");
email = appResponse.getJSON("Email");
// fetch job details
job_id = appResponse.getJSON("Job_Opening_ID");
job = appResponse.getJSON("Job_Opening_Name");
user_job = replaceAll(job," ","+");
// append sent status with the job id
initialSTATUS = "SENT";
getSUFFIX = job_id.getSuffix("ZR");
msgSTATUS = initialSTATUS.concat(getSUFFIX).toString();
// fetch candidate ID for verification
cand_id = appResponse.getJSON("LEADID");
appID = appResponse.getJSON("Application_ID");
linkHeader = "https://recruit.zoho.com/recruit/v2/Candidates";
candidate_link = linkHeader.concat("/").concat(cand_id);
//
//
// check if WA number is not null
if(phNo != null)
{
	//verify phNO using whatsapp to check if the number is valid or not
	//if the number is valid proceed with the below step and change the WA_NUM_STATUS to valid
	//if the number is not valid change the WA_NUM_STATUS to invalid
	//also check if the status code is 200 and if it is not, send an email saying the same
	// prepare number for verification
	waVerify = "+".concat(phNo);
	// pass number to Zoho API
	paramMap = Map();
	blocking = "wait";
	number = waVerify;
	contactArr = {number};
	force_check = true;
	paramMap.put("blocking",blocking);
	paramMap.put("contacts",contactArr);
	paramMap.put("force_check",force_check);
	//info paramMap.toString();
	ver_response = invokeurl
	[
		url :"https://waba.360dialog.io/v1/contacts"
		type :POST
		parameters:paramMap.toString()
		detailed:true
		connection:"dialogverify"
		content-type:"application/json"
	];
	//
	//
	// fetch important keys
	verResponseCode = ver_response.getJSON("responseCode");
	verResponse_main = ver_response.getJSON("responseText");
	verResponse_main_contacts = verResponse_main.getJSON("contacts");
	numStatus = verResponse_main_contacts.getJSON("status");
	if(verResponseCode == 200)
	{
		if(numStatus == "valid")
		{
			//change WA_NUM_STATUS to valid
			paramMap = Map();
			status = {"WA_NUM_STATUS":numStatus};
			statusArr = {status};
			paramMap.put("data",statusArr);
			//info paramMap.toString();
			response = invokeurl
			[
				url :candidate_link
				type :PUT
				parameters:paramMap.toString()
				connection:"zohorecruitapp"
				content-type:"application/json"
			];
			//
			//
			// BEGIN TRANSACTIONAL BROADCAST
			// pass request url to Zoho API
			cand_response = invokeurl
			[
				url :candidate_link
				type :GET
				connection:"zohorecruitapp"
			];
			//
			//
			// fetch important keys
			candResponse = cand_response.getJSON("data");
			waMSG = candResponse.getJSON("WA_MSG_STATUS");
			//
			//
			// first application
			if(waMSG == null)
			{
				// pass values to engati API
				engati_response = invokeurl
				[
					url :"https://api.engati.com/bot-api/v1.0/customer/67975/bot/6b92dfc86e1e4a17/flow/EA96C8E58482410B8A4ED1858741F354?user.first_name=" + user_name + "&user.user_name=" + phNo + "&applied_job=" + user_job
					type :POST
					detailed:true
					connection:"api_trigger"
				];
				engatiResponse = engati_response.getJSON("responseCode");
				//info engati_response;
				if(engatiResponse == 200)
				{
					paramMap = Map();
					status = {"WA_MSG_STATUS":msgSTATUS};
					statusArr = {status};
					paramMap.put("data",statusArr);
					//info paramMap.toString();
					response = invokeurl
					[
						url :candidate_link
						type :PUT
						parameters:paramMap.toString()
						connection:"zohorecruitapp"
						content-type:"application/json"
					];
				}
			}
			// applications post the first one
			else if(waMSG != null)
			{
				// pass values to engati API
				engati_response = invokeurl
				[
					url :"https://api.engati.com/bot-api/v1.0/customer/67975/bot/6b92dfc86e1e4a17/flow/AF86431251264794A4F18B83FC75909B?user.first_name=" + user_name + "&user.user_name=" + phNo + "&applied_job=" + user_job
					type :POST
					detailed:true
					connection:"api_trigger"
				];
				engatiResponse = engati_response.getJSON("responseCode");
				//info engati_response;
				if(engatiResponse == 200)
				{
					paramMap = Map();
					status = {"WA_MSG_STATUS":msgSTATUS};
					statusArr = {status};
					paramMap.put("data",statusArr);
					//info paramMap.toString();
					response = invokeurl
					[
						url :candidate_link
						type :PUT
						parameters:paramMap.toString()
						connection:"zohorecruitapp"
						content-type:"application/json"
					];
					// send email about the candidate to geoffry@ziphire.hr
					mailSUBJECT = "DELUGE APPLICATION ALERT";
					mailID = "geoffry@ziphire.hr";
					ini_applicationLINK = "https://recruit.zoho.com/recruit/org777718150/EntityInfo.do?module=Candidates&id";
					applicationLINK = ini_applicationLINK.concat("=").concat(application_id).concat("&submodule=Applications");
					// send mail
					sendmail
					[
						from :zoho.adminuserid
						to :mailID
						subject :mailSUBJECT
						message :"<div style='background-color: rgb(255, 255, 255); margin: auto; font: normal 14px lucida grande, lucida sans, lucida sans unicode, arial, helvetica, verdana, sans-serif; border: 1px solid rgb(227, 227, 227); width: 600px; padding: 30px'><div style='font-size: 21px; color: rgb(34, 34, 34); float: left; padding-top: 10px; width: 65%'><span class='size' style='font-size:16px'>Dear Geoffry, </span> <br></div><div style='color: rgb(118, 118, 118); line-height: 27px'><div style='color: rgb(68, 68, 68)'><div style='padding-top: 24px; font-size: 15px'><div><br></div><div><b><span class='size' style='font-size: 16px'>" + full_name + " (" + appID + ") </span></b><span class='size' style='font-size: 16px'> has applied to another role, </span><b><span class='size' style='font-size: 16px'> " + job + " (" + job_id + ") </span></b><span class='size' style='font-size: 16px'>. Please click on the below button to access the application page on Zoho Recruit.</span> <br></div><div><br></div><div class='align-center' style='text-align: center;'><span class='size' style='font-size:16px'><a href=" + applicationLINK + " style='display: inline-block; padding: 5px; border-radius: 8px; background-color: rgb(37, 211, 102); color: rgb(255, 255, 255); text-decoration: none; font-family: sans-serif; font-size: 16px' target='_blank'><b>ACCESS APPLICATION </b> </a> </span> <br></div><div class='align-center' style='text-align: center;'><br></div><div class='align-left' style='text-align: left;'><span class='size' style='font-size: 16px'>Please send test to the above user.</span> <br></div><div class='align-left' style='text-align: left;'><br></div><div class='align-left' style='text-align: left;'><br></div><div class='align-left' style='text-align: left;'><br></div><div><span class='size' style='font-size:16px'>Best Regards, </span> <br></div><div><span class='size' style='font-size:16px'>Ziphire HR </span> <br></div></div></div></div></div><div><br></div>"
					]
				}
			}
		}
		else if(numStatus == "invalid")
		{
			//change WA_NUM_STATUS to invalid
			paramMap = Map();
			status = {"WA_NUM_STATUS":numStatus};
			statusArr = {status};
			paramMap.put("data",statusArr);
			//info paramMap.toString();
			response = invokeurl
			[
				url :candidate_link
				type :PUT
				parameters:paramMap.toString()
				connection:"zohorecruitapp"
				content-type:"application/json"
			];
			// send email to candidate to update their WhatsApp number
			fromMailID = "jobs@ziphire.me";
			tomailID = email;
			mailSubject = "Ziphire HR - Update WhatsApp Information!";
			candidate_portal = "https://ziphire.zohorecruit.com/candidateportal";
			/*
			sendmail
			[
				from :
				to :tomailID
				subject :mailSubject
				message :"test"
			]
			*/
		}
	}
}
else if(phNo == null)
{
	//change WA_NUM_STATUS to NA
	paramMap = Map();
	status = {"WA_NUM_STATUS":"NA"};
	statusArr = {status};
	paramMap.put("data",statusArr);
	//info paramMap.toString();
	response = invokeurl
	[
		url :candidate_link
		type :PUT
		parameters:paramMap.toString()
		connection:"zohorecruitapp"
		content-type:"application/json"
	];
}